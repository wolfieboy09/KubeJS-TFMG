import groovy.json.JsonSlurper

if (project.rootProject.hasProperty("secretFile")) {
    project.logger.lifecycle("Automatically loading properties from the secretFile")
    final def secretsFile = project.rootProject.file(project.rootProject.getProperty("secretFile"))

    if (secretsFile.exists() && secretsFile.name.endsWith(".json")) {
        loadProperties(secretsFile)
    }
}

def loadProperties(propertyFile) {
    if (propertyFile.exists()) {
        propertyFile.withReader {
            Map propMap = new JsonSlurper().parse it

            for (entry in propMap) {
                if (!entry.key.endsWith("_comment")) {
                    project.ext.set(entry.key, entry.value)
                }
            }

            project.logger.lifecycle("Successfully loaded " + propMap.size() + " properties")
            propMap.clear()
        }
    } else {
        project.logger.warn("The property file " + propertyFile.getName() + " could not be loaded. It does not exist.")
    }
}

ext {
    loadProperties = this.&loadProperties
}